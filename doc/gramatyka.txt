/**********************************************************/
/*      Gramatyka Barneksa-Patyka w ujęciu Jogiego        */
/*                    wersja 1.0.2                        */
/*                                                        */
/* http://www-cgi.uni-regensburg.de/~brf09510/syntax.html */
/**********************************************************/
/* Wykorzystano następujące wbudowane tokeny biblioteki   */
/* pyparsing:                                             */
/*                                                        */
/*  - Word(alphanums) - słowo złożone ze znaków           */
/*    alfanumerycznych                                    */
/*  - Word(alphanums+'-_') - j.w., uzupełnione o znaki    */
/*    podkreślenia i myślnik                              */
/*  - quotedString - dowolny napis zamknięty w            */
/*    cudzysłowach pojedynczym lub podwójnym              */
/**********************************************************/

grammar = {definition}
definition = header "\\n" {element "\\n"}

header = [prototype] parent_name [object_name]
parent_name = name
object_name = name

element = operation | attribute | property

operation = [static] scope method_name '(' [parameters] ')' [':' return_type]
method_name = name
return_type = name
parameters = {parameter ','} parameter
parameter = parameter_name ':' parameter_type
parameter_name = name
paramter_type = name

attribute = [static] scope attribute_name [':' attribute_type] ['=' attribute_default]
attribute_name = name
attribute_type = name
attribute_default = attribute_value
attribute_value = number | string

property = property_name ':' property_values
property_name = name

/*uproszczenie garmatyki - property moze miec 1 wartosc*/
/*property_values = {property_value} property_value*/
property_values = property_value

property_value = multiplicity | name | string

multiplicity = (number|'*') ".." (number|'*')
number = "Word(nums)"
name = "Word(alphanums+'_-')"
string = "quotedString"

scope = '+' | '-' | '#' | '~'
static = "_"
/*błędnie generowany diagram - nie mogą istnieć różne tokeny o takiej samej wartości.*/
prototype = "prototype"